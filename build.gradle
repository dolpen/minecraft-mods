buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

// Gradle Plugin
apply plugin: 'net.minecraftforge.gradle.forge'


// MOD Configuration
version = ""
group = "net.dolpen.mcmods"
archivesBaseName = "dolpenext"

// javac

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
    options.encoding = "UTF-8"
    sourceCompatibility = targetCompatibility = "1.8"
}
compileTestJava.options.encoding = "UTF-8"


// MCP / Forge
minecraft {
    version = "1.12.2-14.23.5.2768"
    runDir = "run"
    mappings = "snapshot_20171003"
}

// Develop / Test Configuration

runClient {
    jvmArgs "-Xms1g", "-Xmx1g"
}

runServer {
    jvmArgs "-Xms1g", "-Xmx1g"
    args "nogui"
}

test {
    useJUnitPlatform()
}


dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.3.1"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.3.1"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar.archiveName = "dolpenext.jar"